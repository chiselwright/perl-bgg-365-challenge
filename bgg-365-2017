#!/usr/bin/env perl
use strict;
use warnings;

use feature ':5.16';

# sudo aptitude install libxml2
# cpanm XML::LibXML

use DateTime;
use LWP::UserAgent;
use XML::LibXML;

# curl 'https://www.boardgamegeek.com/xmlapi2/plays?username=chizcw&mindate=2017-01-01&maxdate=2017-12-31'
my $user    = $ARGV[0] || 'chizcw';
my $year    = 2017;


sub fetch_xml {
    my $api_url = sprintf(
        'https://www.boardgamegeek.com/xmlapi2/plays?username=%s&mindate=%d-01-01&maxdate=%d-12-31',
        $user,
        $year,
        $year
    );

    my $ua = LWP::UserAgent->new(agent => "bgg-365-2017/0.01");
    my $response = $ua->get($api_url);
    die "couldn't fetch xml\n" unless $response && $response->is_success;
    my $xmlString = $response->content;

    return $xmlString;
}


#warn $api_url;
my $dom = XML::LibXML->load_xml(string => fetch_xml);

# there should only be one, but let's play it safe
foreach my $plays_node ($dom->findnodes('/plays')) {
    my $user  = $plays_node->getAttribute('username');
    my $plays = $plays_node->getAttribute('total');

    say sprintf("[u][b]%s[/b][/u]\n", $user);

    # output stars as simple yes/no for days in year
    say sprintf("\n[u][b]%s[/b][/u]\n", "365 Plays Progress");
    my $count = 365;
    for (my $i = 0; $i < 365; $i++) {
        if ($i < $plays) {
            print ':star: ';
        }
        else {
            print ':nostar: ';
        }
    }
    print "\n";

    my %tally_for_date=();
    foreach my $play_node ($plays_node->findnodes('/plays/play')) {
        my $date = $play_node->getAttribute('date');
        $tally_for_date{$date}++;
    }

    say sprintf("\n[u][b]%s[/b][/u]\n", "365 Days Played");
    foreach my $month (1 .. 12) {
        my $ldom = DateTime->last_day_of_month(year => $year, month => $month);
        printf("[c]%s: [/c]", $ldom->month_abbr);

        foreach my $day (1 .. $ldom->day) {
            my $dt = $ldom->clone->set_day( $day );
            if (exists $tally_for_date{ $dt->ymd }) {
                print ':star: ';
            }
            else {
                print ':nostar: ';
            }
        }
        print "\n";
    }
}

