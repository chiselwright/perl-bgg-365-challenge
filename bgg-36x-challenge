#!/usr/bin/env perl
use strict;
use warnings;

use feature ':5.16';

# sudo aptitude install libxml2
# cpanm XML::LibXML

use DateTime;
use LWP::UserAgent;
use XML::LibXML;

# curl 'https://www.boardgamegeek.com/xmlapi2/plays?username=chizcw&mindate=2017-01-01&maxdate=2017-12-31'
my $user        = $ARGV[0] || 'chizcw';
my $year        = $ARGV[1] || 2017;
my $days_in_year= 365 + DateTime->new(year=>$year, month=>1, day=>1)->is_leap_year;

sub fetch_xml {
    my $page = shift || 1;

    my $api_url = sprintf(
        'https://www.boardgamegeek.com/xmlapi2/plays?username=%s&mindate=%d-01-01&maxdate=%d-12-31&page=%d',
        $user,
        $year,
        $year,
        $page
    );
    #warn "$api_url\n";

    my $ua = LWP::UserAgent->new(agent => "bgg-365-2017/0.01");
    my $response = $ua->get($api_url);
    die "couldn't fetch xml\n" unless $response && $response->is_success;
    my $xmlString = $response->content;

    return $xmlString;
}

sub emit_section_title {
    my $title = shift;
    say sprintf("\n[u][b]%s[/b][/u]\n", $title);
}

sub emit_plays_logged_section {
    my $plays = shift;

    emit_section_title($user);

    # output stars as simple yes/no for days in year
    emit_section_title(
        sprintf('%d Plays Logged in %d', $plays, $year)
    );
    for (my $i = 0; $i < $days_in_year; $i++) {
        if ($i < $plays) {
            print ':star: ';
        }
        else {
            print ':nostar: ';
        }
    }
    print "\n";
}

sub emit_days_played_section {
    my $tally_ref = shift;
    my %tally_for_date = %{ $tally_ref };

    emit_section_title(
        sprintf('%s Days Played in %d', $days_in_year, $year)
    );

    foreach my $month (1 .. 12) {
        my $ldom = DateTime->last_day_of_month(year => $year, month => $month);
        printf("[c]%s: [/c]", $ldom->month_abbr);

        foreach my $day (1 .. $ldom->day) {
            my $dt = $ldom->clone->set_day( $day );
            if (exists $tally_for_date{ $dt->ymd }) {
                print ':star: ';
            }
            else {
                print ':nostar: ';
            }
        }
        print "\n";
    }
}

sub emit_unique_games_played_section {
    my $tally_ref = shift;
    my %tally_for_game = %{ $tally_ref };
    my $unique_tally = scalar keys %tally_for_game;

    # output stars as simple yes/no for unique games played
    emit_section_title(
        sprintf('%s Unique Games Played in %d', $unique_tally, $year)
    );
    for (my $i = 0; $i < $days_in_year; $i++) {
        if ($i < $unique_tally) {
            print ':star: ';
        }
        else {
            print ':nostar: ';
        }
    }
    print "\n";
}



#warn $api_url;
my $dom = XML::LibXML->load_xml(string => fetch_xml);

# there should only be one, but let's play it safe
foreach my $plays_node ($dom->findnodes('/plays')) {
    my $user  = $plays_node->getAttribute('username');
    my $plays = $plays_node->getAttribute('total');



    # we only get 100 results per page, so if there are more than 100 plays we
    # need to loop over the fetch() enough times to grab all the data for the
    # year
    # e.g. int((plays+100) / 100)
    my $num_pages = int(($plays+100) / 100);
    my %tally_for_date=();
    my %tally_for_game=();

    for (my $i=1; $i<=$num_pages; $i++) {
        my $plays_dom = XML::LibXML->load_xml(string => fetch_xml($i));
        # add the plays by date
        foreach my $play_node ($plays_dom->findnodes('/plays/play')) {
            my $date = $play_node->getAttribute('date');
            $tally_for_date{$date}++;
        }
        # add the plays by game (id)
        foreach my $item_node ($plays_dom->findnodes('/plays/play/item')) {
            my $item_id     = $item_node->getAttribute('objectid');
            my $item_name   = $item_node->getAttribute('name');
            $tally_for_game{ $item_id }{name} = $item_name;
            $tally_for_game{ $item_id }{plays}++;
        }
    }

    emit_plays_logged_section($plays);
    emit_days_played_section(\%tally_for_date);
    emit_unique_games_played_section(\%tally_for_game);

    #use Data::Dump 'pp'; warn pp(\%tally_for_game);
}
